Acrobatic Cider Cougar

High

# Incorrect return value of the `VaultLib._redeemCtDsAndSellExcessCt()` function

## Summary

In the `VaultLib._redeemCtDsAndSellExcessCt()` function, `PsmLibrary.lvRedeemRaWithCtDs(self, redeemAmount, dsId)` is called to redeem `RA` using `(CT + DS) * redeemAmount`. In this case, the redeemed amount of `RA` is `redeemAmount * exchangeRate`. However, it accounts the redeemed amount of `RA` as `redeemAmount` instead of `redeemAmount * exchangeRate`.

## Vulnerability Detail

As noted at line 332 of the `_redeemCtDsAndSellExcessCt()` function, it redeems `RA` using `(CT + DS) * redeemAmount`. Based on the exchange rate between `RA` and `CT + DS`, the redeemed amount of `RA` should equal `redeemAmount * ds.exchangeRate()`. However, at line 331, only `redeemAmount`—instead of `redeemAmount * ds.exchangeRate()`—is added to `ra`. Consequently, the `_redeemCtDsAndSellExcessCt()` function miscalculates the redeemed amount of `RA`, resulting in a smaller value than intended. As a result, users will receive less `RA` than they should when redeeming `lv` before expiry, since the `redeemEarly()` function uses `_redeemCtDsAndSellExcessCt()` to determine the amount of `RA` users will receive (through [redeemEarly](https://github.com/sherlock-audit/2024-08-cork-protocol/tree/main/Depeg-swap/contracts/libraries/VaultLib.sol#L656) -> [_liquidateLpPartial](https://github.com/sherlock-audit/2024-08-cork-protocol/tree/main/Depeg-swap/contracts/libraries/VaultLib.sol#L315) -> [_redeemCtDsAndSellExcessCt](https://github.com/sherlock-audit/2024-08-cork-protocol/tree/main/Depeg-swap/contracts/libraries/VaultLib.sol#L331-L332)).

```solidity
    function _redeemCtDsAndSellExcessCt(
        State storage self,
        uint256 dsId,
        IUniswapV2Router02 ammRouter,
        IDsFlashSwapCore flashSwapRouter,
        uint256 ammCtBalance
    ) internal returns (uint256 ra) {
        uint256 reservedDs = flashSwapRouter.getLvReserve(self.info.toId(), dsId);

        uint256 redeemAmount = reservedDs >= ammCtBalance ? ammCtBalance : reservedDs;

        reservedDs = flashSwapRouter.emptyReservePartial(self.info.toId(), dsId, redeemAmount);

331     ra += redeemAmount;
332     PsmLibrary.lvRedeemRaWithCtDs(self, redeemAmount, dsId);

        uint256 ctSellAmount = reservedDs >= ammCtBalance ? 0 : ammCtBalance - reservedDs;

        DepegSwap storage ds = self.ds[dsId];
        address[] memory path = new address[](2);
        path[0] = ds.ct;
        path[1] = self.info.pair1;

        ERC20(ds.ct).approve(address(ammRouter), ctSellAmount);

        if (ctSellAmount != 0) {
            // 100% tolerance, to ensure this not fail
            ra += ammRouter.swapExactTokensForTokens(ctSellAmount, 0, path, address(this), block.timestamp)[1];
        }
    }
```

## Impact

Loss of `RA` for users.

## Code Snippet

https://github.com/sherlock-audit/2024-08-cork-protocol/tree/main/Depeg-swap/contracts/libraries/VaultLib.sol#L318-L347

## Tool used

Manual Review

## Recommendation

Consider the exchange rate.

```diff
    function _redeemCtDsAndSellExcessCt(
        State storage self,
        uint256 dsId,
        IUniswapV2Router02 ammRouter,
        IDsFlashSwapCore flashSwapRouter,
        uint256 ammCtBalance
    ) internal returns (uint256 ra) {
        uint256 reservedDs = flashSwapRouter.getLvReserve(self.info.toId(), dsId);

        uint256 redeemAmount = reservedDs >= ammCtBalance ? ammCtBalance : reservedDs;

        reservedDs = flashSwapRouter.emptyReservePartial(self.info.toId(), dsId, redeemAmount);

-       ra += redeemAmount;
+       ra += redeemAmount * self.ds[dsId].exchangeRate();
        PsmLibrary.lvRedeemRaWithCtDs(self, redeemAmount, dsId);

        uint256 ctSellAmount = reservedDs >= ammCtBalance ? 0 : ammCtBalance - reservedDs;

        DepegSwap storage ds = self.ds[dsId];
        address[] memory path = new address[](2);
        path[0] = ds.ct;
        path[1] = self.info.pair1;

        ERC20(ds.ct).approve(address(ammRouter), ctSellAmount);

        if (ctSellAmount != 0) {
            // 100% tolerance, to ensure this not fail
            ra += ammRouter.swapExactTokensForTokens(ctSellAmount, 0, path, address(this), block.timestamp)[1];
        }
    }
```