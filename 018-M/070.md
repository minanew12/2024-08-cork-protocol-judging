Joyful Obsidian Wolf

Medium

# Possible division by zero

### Summary

[separateLiquidity](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/MathHelper.sol#L128) function has `ratePerLv ` calculation
`ratePerLv = ((totalAmount * 1e18) / totalLvIssued);` 

### Root Cause

_No response_

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

The problem is that if `totalLvIssued` is 0 this will lead to division by zero error. Releated with this problem the function [redeemExpired](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/VaultLib.sol#L514) will revert. If a division by zero occurs, it will prevent the function from completing the redemption process.

### PoC

```solidity
 function separateLiquidity(uint256 totalAmount, uint256 totalLvIssued, uint256 totalLvWithdrawn)
        external
        pure
        returns (uint256 attributedWithdrawal, uint256 attributedAmm, uint256 ratePerLv)
    {
        // with 1e18 precision
        ratePerLv = ((totalAmount * 1e18) / totalLvIssued);  <--

        // attribute all to AMM if no lv issued or withdrawn
        if (totalLvIssued == 0 || totalLvWithdrawn == 0) {
            return (0, totalAmount, ratePerLv);
        }

        attributedWithdrawal = (ratePerLv * totalLvWithdrawn) / 1e18;
        attributedAmm = totalAmount - attributedWithdrawal;

        assert((attributedWithdrawal + attributedAmm) == totalAmount);
    }
```
```solidity
 function reserve(VaultPool storage self, uint256 totalLvIssued, uint256 addedRa, uint256 addedPa) internal {
        uint256 totalLvWithdrawn = self.withdrawalPool.atrributedLv;
        self.withdrawalPool.atrributedLv = totalLvWithdrawn;

        // RA
        uint256 totalRa = self.withdrawalPool.raBalance + addedRa;
        (uint256 attributedToWithdraw, uint256 attributedToAmm, uint256 ratePerLv) =
            MathHelper.separateLiquidity(totalRa, totalLvIssued, totalLvWithdrawn);

        self.withdrawalPool.raBalance = attributedToWithdraw;
        self.ammLiquidityPool.balance = attributedToAmm;
        self.withdrawalPool.raExchangeRate = ratePerLv;

        // PA
        uint256 totalPa = self.withdrawalPool.paBalance + addedPa;
        (attributedToWithdraw, attributedToAmm, ratePerLv) =
            MathHelper.separateLiquidity(totalPa, totalLvIssued, totalLvWithdrawn);

        self.withdrawalPool.paBalance = attributedToWithdraw;
        self.withdrawalPool.stagnatedPaBalance = attributedToWithdraw;
        self.withdrawalPool.paExchangeRate = ratePerLv;

        assert(totalRa == self.withdrawalPool.raBalance + self.ammLiquidityPool.balance);
    }
```
```solidity
 function _liquidatedLp(
        State storage self,
        uint256 dsId,
        IUniswapV2Router02 ammRouter,
        IDsFlashSwapCore flashSwapRouter
    ) internal {
        DepegSwap storage ds = self.ds[dsId];

        // if there's no LP, then there's nothing to liquidate
        if (self.vault.config.lpBalance == 0) {
            return;
        }

        // the following things should happen here(taken directly from the whitepaper) :
        // 1. The AMM LP is redeemed to receive CT + RA
        // 2. Any excess DS in the LV is paired with CT to redeem RA
        // 3. The excess CT is used to claim RA + PA in the PSM
        // 4. End state: Only RA + redeemed PA remains

        self.vault.lpLiquidated.set(dsId);

        (uint256 raAmm, uint256 ctAmm) = __liquidateUnchecked(
            self, self.info.pair1, self.ds[dsId].ct, ammRouter, IUniswapV2Pair(ds.ammPair), self.vault.config.lpBalance
        );

        uint256 reservedDs = flashSwapRouter.emptyReserve(self.info.toId(), dsId);

        uint256 redeemAmount = reservedDs >= ctAmm ? ctAmm : reservedDs;
        PsmLibrary.lvRedeemRaWithCtDs(self, redeemAmount, dsId);

        // if the reserved DS is more than the CT that's available from liquidating the AMM LP
        // then there's no CT we can use to effectively redeem RA + PA from the PSM
        uint256 ctAttributedToPa = reservedDs >= ctAmm ? 0 : ctAmm - reservedDs;

        uint256 psmPa;
        uint256 psmRa;

        if (ctAttributedToPa != 0) {
            (psmPa, psmRa) = PsmLibrary.lvRedeemRaPaWithCt(self, ctAttributedToPa, dsId);
        }

        psmRa += redeemAmount;

        self.vault.pool.reserve(self.vault.lv.totalIssued(), raAmm + psmRa, psmPa);
    }
```

```solidity
 function redeemExpired(
        State storage self,
        address owner,
        address receiver,
        uint256 amount,
        IUniswapV2Router02 ammRouter,
        IDsFlashSwapCore flashSwapRouter,
        bytes memory rawLvPermitSig,
        uint256 deadline
    ) external returns (uint256 attributedRa, uint256 attributedPa) {
        {
            uint256 dsId = self.globalAssetIdx;
            DepegSwap storage ds = self.ds[dsId];

            uint256 userEligible = self.vault.pool.withdrawEligible[owner];

            if (userEligible == 0 && !ds.isExpired()) {
                revert Unauthorized(owner);
            }

            // user can only redeem up to the amount they requested, when there's a DS active
            // if there's no DS active, then there's no cap on the amount of LV that can be redeemed
            if (!ds.isExpired() && userEligible < amount) {
                revert InsufficientBalance(owner, amount, userEligible);
            }

            if (ds.isExpired() && !self.vault.lpLiquidated.get(dsId)) {
                _liquidatedLp(self, dsId, ammRouter, flashSwapRouter);
                assert(self.vault.balances.ra.locked == 0);
            }
        }

        uint256 burnUserAmount;
        uint256 burnSelfAmount;

        (attributedRa, attributedPa, burnUserAmount, burnSelfAmount) = self.vault.pool.redeem(amount, owner);
        assert(burnSelfAmount + burnUserAmount == amount);

        if (deadline != 0) {
            DepegSwapLibrary.permit(
                self.vault.lv._address, rawLvPermitSig, owner, address(this), burnUserAmount, deadline
            );
        }
        //ra
        IERC20(self.info.pair1).safeTransfer(receiver, attributedRa);
        //pa
        IERC20(self.info.pair0).safeTransfer(receiver, attributedPa);

        self.vault.lv.burnSelf(burnSelfAmount);

        if (burnUserAmount != 0) {
            ERC20Burnable(self.vault.lv._address).burnFrom(owner, burnUserAmount);
        }
    }
```

### Mitigation

Moove this check ` if (totalLvIssued == 0 || totalLvWithdrawn == 0) {
            return (0, totalAmount, ratePerLv);
        }` beffore `ratePerLv` calculation

```solidity
function separateLiquidity(uint256 totalAmount, uint256 totalLvIssued, uint256 totalLvWithdrawn)
        external
        pure
        returns (uint256 attributedWithdrawal, uint256 attributedAmm, uint256 ratePerLv)
    {   
        // attribute all to AMM if no lv issued or withdrawn
        if (totalLvIssued == 0 || totalLvWithdrawn == 0) {
            return (0, totalAmount, ratePerLv);
        }

        // with 1e18 precision
        ratePerLv = ((totalAmount * 1e18) / totalLvIssued);


        attributedWithdrawal = (ratePerLv * totalLvWithdrawn) / 1e18;
        attributedAmm = totalAmount - attributedWithdrawal;

        assert((attributedWithdrawal + attributedAmm) == totalAmount);
    }
```