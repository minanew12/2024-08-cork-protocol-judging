Acrobatic Cider Cougar

High

# Title: The `DsFlashSwap.getAmountOutSellDS()` function considers the exchange rate between `RA` and `CT + DS` to be `1:1`

## Summary

The `DsFlashSwap.getAmountOutSellDS()` function considers the exchange rate between `RA` and `CT + DS` to be `1:1`.

## Vulnerability Detail

Let's consider the scenario where users sell `DS`.

The algorithm is as follows (according to the documentation):

1. Seller sends Depeg Swap into a swap contract
2. The contract borrows an equivalent amount of Cover Token from the pool
3. The Depeg Swap and Cover Token are used to redeem the Redemption Asset
4. Redemption Asset is sent to the seller (or routed to any major tokens)
5. A portion of the Redemption Asset is sold to the pool for Cover Token to return the amount from step 2


The `FlashSwapRouter.__swapDsforRa()` function calculates `amountOut`, which represents the amount of `RA` users will receive, by using the `DsFlashSwap.getAmountOutSellDS()` function (line 295).

```solidity
    function __swapDsforRa(
        AssetPair storage assetPair,
        Id reserveId,
        uint256 dsId,
        uint256 amount,
        uint256 amountOutMin
    ) internal returns (uint256 amountOut) {
295     (amountOut,) = assetPair.getAmountOutSellDS(amount);

        if (amountOut < amountOutMin) {
            revert InsufficientOutputAmount();
        }

        __flashSwap(assetPair, assetPair.pair, 0, amount, dsId, reserveId, false, amountOut);
    }
```

In the `DsFlashSwap.getAmountOutSellDS()` function:

- `amount` refers to the quantity of `DS` the user sells.
- `repaymentAmount` is the amount of `RA` that must be repaid to the pool in step 5.

In step 3, `amount * (CT + DS)` is exchanged for `RA`. Therefore, the amount of `RA` exchanged is `amount * exchangeRate`. Consequently, the total amount of `RA` the user should receive is `amount * exchangeRate - repaymentAmount`. However, the `DsFlashSwap.getAmountOutSellDS()` function calculates this as `amount - repaymentAmount`, as seen in line 170. This discrepancy results in a loss of funds for users when the `exchangeRate` exceeds 1.

```solidity
    function getAmountOutSellDS(AssetPair storage assetPair, uint256 amount)
        internal
        view
        returns (uint256 amountOut, uint256 repaymentAmount)
    {
        (uint112 raReserve, uint112 ctReserve) = getReservesSorted(assetPair);
        // we calculate the repayment amount based on the imbalanced ct reserve since we borrow CT from the AMM
        repaymentAmount = MinimalUniswapV2Library.getAmountIn(amount, raReserve, ctReserve - amount);

        // the amountOut is essentially what the user receive, we can calculate this by simply subtracting the repayment amount
        // from the amount, since we're getting back the same RA amount as DS user buy, this works. to get the effective price per DS,
        // you would devide this by the DS amount user bought.
        // note that we subtract 1 to enforce uni v2 rules
170     amountOut = amount - repaymentAmount;

        // enforce uni v2 rules, pay 1 wei more
        amountOut -= 1;
        repaymentAmount += 1;

        assert(amountOut + repaymentAmount == amount);
    }
```

## Impact

Loss of `RA` for the users.

## Code Snippet

https://github.com/sherlock-audit/2024-08-cork-protocol/tree/main/Depeg-swap/contracts/core/flash-swaps/FlashSwapRouter.sol#L288-L302

https://github.com/sherlock-audit/2024-08-cork-protocol/tree/main/Depeg-swap/contracts/libraries/DsFlashSwap.sol#L157-L177

## Tool used

Manual Review

## Recommendation

The `getAmountOutSellDS()` function should accurately account for the exchange rate between `RA` and `CT + DS`.

```diff
    function getAmountOutSellDS(AssetPair storage assetPair, uint256 amount)
        internal
        view
        returns (uint256 amountOut, uint256 repaymentAmount)
    {
        (uint112 raReserve, uint112 ctReserve) = getReservesSorted(assetPair);
        // we calculate the repayment amount based on the imbalanced ct reserve since we borrow CT from the AMM
        repaymentAmount = MinimalUniswapV2Library.getAmountIn(amount, raReserve, ctReserve - amount);

        // the amountOut is essentially what the user receive, we can calculate this by simply subtracting the repayment amount
        // from the amount, since we're getting back the same RA amount as DS user buy, this works. to get the effective price per DS,
        // you would devide this by the DS amount user bought.
        // note that we subtract 1 to enforce uni v2 rules
-       amountOut = amount - repaymentAmount;
+       amountOut = amount * assetPair.ds.exchangeRate() - repaymentAmount;

        // enforce uni v2 rules, pay 1 wei more
        amountOut -= 1;
        repaymentAmount += 1;

        assert(amountOut + repaymentAmount == amount);
    }
```