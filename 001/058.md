Colossal Magenta Elk

High

# The ModuleCore::issueNewDs() function will revert if the totalSupply of LV tokens is 0

### Summary

The [ModuleCore::initialize()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/ModuleCore.sol#L37-L55) function initializes some parameters and deploys an LV token for the pair of RA and PA tokens provided by the admin. There is only one LV token per RA:PA pair. The CT and DS tokens created via the [ModuleCore::issueNewDs()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/ModuleCore.sol#L57-L86) function for a specific pair of RA and PA tokens, have an expiration period, once they have expired new CT and DS tokens can be created by calling the [ModuleCore::issueNewDs()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/ModuleCore.sol#L57-L86) function, the LV token will be the same. The [ModuleCore::issueNewDs()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/ModuleCore.sol#L57-L86) function internally calls the [VaultLib::onNewIssuance()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/VaultLib.sol#L92-L108) function, which doesn't do anything for the first time it is called for a specific RA and PA pair, but each next time there are several things that the function does, as can be seen from the code snippet below:
```solidity
    function onNewIssuance(
        State storage self,
        uint256 prevDsId,
        IDsFlashSwapCore flashSwapRouter,
        IUniswapV2Router02 ammRouter
    ) external {
        // do nothing at first issuance
        if (prevDsId == 0) {
            return;
        }

        if (!self.vault.lpLiquidated.get(prevDsId)) {
            _liquidatedLp(self, prevDsId, ammRouter, flashSwapRouter);
        }

        __provideAmmLiquidityFromPool(self, flashSwapRouter, self.ds[self.globalAssetIdx].ct, ammRouter);
    }
```
It first has to liquidate all the LP tokens from the AMM for the specific RA and CT tokens, remember that CT tokens expire. In order to do that it calls the [VaultLib::_liquidatedLp()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/VaultLib.sol#L349-L393) function, which burns the LP tokens owned by the contract, does some other stuff which is not important for the context of this issue, and then calls the [VaultPoolLib::reserve()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/VaultPoolLib.sol#L12-L35) function.

```solidity
    function reserve(VaultPool storage self, uint256 totalLvIssued, uint256 addedRa, uint256 addedPa) internal {
        uint256 totalLvWithdrawn = self.withdrawalPool.atrributedLv;
        self.withdrawalPool.atrributedLv = totalLvWithdrawn;

        // RA
        uint256 totalRa = self.withdrawalPool.raBalance + addedRa;
        (uint256 attributedToWithdraw, uint256 attributedToAmm, uint256 ratePerLv) =
            MathHelper.separateLiquidity(totalRa, totalLvIssued, totalLvWithdrawn);

        self.withdrawalPool.raBalance = attributedToWithdraw;
        self.ammLiquidityPool.balance = attributedToAmm;
        self.withdrawalPool.raExchangeRate = ratePerLv;

        // PA
        uint256 totalPa = self.withdrawalPool.paBalance + addedPa;
        (attributedToWithdraw, attributedToAmm, ratePerLv) =
            MathHelper.separateLiquidity(totalPa, totalLvIssued, totalLvWithdrawn);

        self.withdrawalPool.paBalance = attributedToWithdraw;
        self.withdrawalPool.stagnatedPaBalance = attributedToWithdraw;
        self.withdrawalPool.paExchangeRate = ratePerLv;

        assert(totalRa == self.withdrawalPool.raBalance + self.ammLiquidityPool.balance);
    }

```
As can be seen from the above code snippet  the [VaultLib::_liquidatedLp()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/VaultLib.sol#L349-L393) function calls the [MathHelper::separateLiquidity()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/MathHelper.sol#L128-L145) function:
```solidity
    function separateLiquidity(uint256 totalAmount, uint256 totalLvIssued, uint256 totalLvWithdrawn)
        external
        pure
        returns (uint256 attributedWithdrawal, uint256 attributedAmm, uint256 ratePerLv)
    {
        // with 1e18 precision
        ratePerLv = ((totalAmount * 1e18) / totalLvIssued);

        // attribute all to AMM if no lv issued or withdrawn
        if (totalLvIssued == 0 || totalLvWithdrawn == 0) {
            return (0, totalAmount, ratePerLv);
        }

        attributedWithdrawal = (ratePerLv * totalLvWithdrawn) / 1e18;
        attributedAmm = totalAmount - attributedWithdrawal;

        assert((attributedWithdrawal + attributedAmm) == totalAmount);
    }
```
As can be seen from the code above if there is no LV tokens minted the function will revert, and thus deploying new DS and CT tokens for a pair of RA and PA assets won't be possible plus some tokens will be lost as the liquidation of LP tokens will revert. There are various fees collected in the protocol that are deposited in a RA CT UniV2 pair, it is not required LV tokens to be minted for deposits in the AMM pair to be made by the contract. 

### Root Cause
In the [MathHelper::separateLiquidity()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/MathHelper.sol#L128-L145) function, division by 0 is not prevented.

### Internal pre-conditions
There are two highly possible scenarios, why this issue can occur:
1. Simplest is that there were no LV tokens minted. Users can still utilize the main feature of the protocol, which is to hedge against PA assets depegging from the RA asset, without minting LV tokens. When the protocol collects fees and deposits them into the UniV2 pair, it doesn't mint LV tokens.
2. Second scenario is where users have minted LV tokens for previous CT and DS tokens that have already expired. 
    - Let's say for the first issuance of DS and CT tokens, users mint LV tokens. 
    - The CT and DS tokens expire, and new ones are issued. 
    -  So far so good, however now all users redeem their tokens when LV tokens are redeemed they are burned.
    - There are no LV tokens left.

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact
The [ModuleCore::issueNewDs()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/ModuleCore.sol#L57-L86) function will always revert if the totalSupply of LV tokens is 0, and no new DS and CT tokens can be issued. Given the fact that this is one of the most critical functionalities of the protocol, this will make the protocol obsolete. Some of the functions of the protocol collect fees, these fees are then transformed into RA and CT tokens and deposited into a UniV2 pair. Those fees will be locked in the pair forever, as the protocol can't burn the LP token it has. Thus the high severity. 

### PoC

_No response_

### Mitigation

_No response_