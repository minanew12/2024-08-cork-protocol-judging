Colossal Magenta Elk

High

# RA amount is not updated properly in Psm::redeemRaWithDs()

### Summary

The Corck protocol allows user to redeem RA assets with PA and DS assets via the [Psm::redeemRaWithDs()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Psm.sol#L142-L159) function , the protocol charges a fee for that service :
```solidity
    function redeemRaWithDs(Id id, uint256 dsId, uint256 amount)
        external
        override
        nonReentrant
        onlyInitialized(id)
        PSMWithdrawalNotPaused(id)
    {
        State storage state = states[id];
        // gas savings
        uint256 feePrecentage = psmBaseRedemptionFeePrecentage;

        (uint256 received, uint256 _exchangeRate, uint256 fee) =
            state.redeemWithDs(_msgSender(), amount, dsId, bytes(""), 0, feePrecentage);

        VaultLibrary.provideLiquidityWithFee(state, fee, getRouterCore(), getAmmRouter());

        emit DsRedeemed(id, dsId, _msgSender(), amount, received, _exchangeRate, feePrecentage, fee);
    }
```
As can be seen from the above code snippet the [Psm::redeemRaWithDs()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Psm.sol#L142-L159) function first calls the [PsmLib::redeemWithDs()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/PsmLib.sol#L364-L380) function which returns the fee that the protocol charges. The [PsmLib::redeemWithDs()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/PsmLib.sol#L364-L380) function internally calls the [PsmLib::_afterRedeemWithDs()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/PsmLib.sol#L329-L347) function, where the fee is calculated and the amount of RA asset that will be returned to the user is decreased from the internal accounting of the protocol. However the internal accounting for the RA token is decreased by the RA amount that will be sent to user minus the charged fee. Which becomes a problem when the [Psm::redeemRaWithDs()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Psm.sol#L142-L159) function calls the [VaultLib::provideLiquidityWithFee()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/VaultLib.sol#L602-L609) function in order to deposit the fee into the AMM for the CT and RA tokens. After the [VaultLib::provideLiquidityWithFee()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/VaultLib.sol#L602-L609) is called, and after a couple of overengineered internal calls the [VaultLib::__provideLiquidity()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/VaultLib.sol#L153-L172) function will be called which will increase the protocol accounting of the RA token once more. Imagine that a user sends 5e18 PA and 5e18 DS tokens in order to collect 5e18 RA tokens - fees, consider the exchange rate for RA:PA is 1:1, and the fee is 5%. If there is 5 RA tokens in the accounting the user will receive back 4.75e18 RA tokens, and in the accounting we will have 5e18 - 4.75e18 = 0.25e18. However when the  [VaultLib::provideLiquidityWithFee()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/VaultLib.sol#L602-L609) function is called, and we want to provide 0.25e18 RA into the AMM, the amount first have to be split based on the CT:RA ratio of the AMM, if it is equally split, then another 0.125e18 will be added to the internal accounting keeping track of the RA in the system, which is incorrect. This leads to a mismatch between the accounting balance and the actual token balance. This will be problematic when users try to withdraw after the DC and CT tokens have expired, or new CT and DS tokens are issued by the protocol for the existing pair of RA and PA.

### Root Cause

The internal accounting for the RA tokens is incorrectly updated in the [PsmLib::_afterRedeemWithDs()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/PsmLib.sol#L329-L347) function, the call to the [PsmLib::_afterRedeemWithDs()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/PsmLib.sol#L329-L347) function originates from the [Psm::redeemRaWithDs()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Psm.sol#L142-L159) function

### Internal pre-conditions

1. Users deposit some RA tokens via the [Psm::depositPsm()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Psm.sol#L90-L101) function in order to receive back CT and DS tokens
2. Users want to redeem their RA tokens via the [Psm::redeemRaWithDs()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Psm.sol#L142-L159) function, let's say a depeg happened and now 1e18 Pa token is equal to 0.8e18 RA token(the depeg event is just for context). 

### External pre-conditions

_No response_

### Attack Path

There is no attack path, the protocol just functions in this way, as demonstrated in the provided a POC

### Impact
The RA accounting is not properly updated. Which leads to a mismatch between the accounting balance and the actual token balance. This will be problematic when users try to withdraw after  the DC and CT tokens have expired. The first users that withdraw via the [Psm::redeemWithCT()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Psm.sol#L198-L210) function, will receive more tokens than they should, and the last users to withdraw will receive much less tokens that they should, if any. This results in the first users that withdraw, effectively stealing funds from the last users that withdraw. There will also be problems when it comes to calculating the rewards for LV holders, and how much assets should be deposited into a newly created UniV2 pair, for DS and CT tokens that were just issued (the UniV2 pair consists of RA:CT tokens).

### PoC
[Gist](https://gist.github.com/AtanasDimulski/3f9bfc84c63e1c977b877613b644c0e2)
After following the steps in the above mentioned [gist](https://gist.github.com/AtanasDimulski/3f9bfc84c63e1c977b877613b644c0e2) add the following test to the ``AuditorTests.t.sol`` file:

```solidity
    function test_RedeemRaWithDsIncorectRaUpdate() public {
        vm.startPrank(alice);
        WETH.mint(alice, 10e18);
        peggedAsset.mint(alice, 5e18);
        peggedAsset.approve(address(moduleCore), type(uint256).max);
        WETH.approve(address(moduleCore), type(uint256).max);
        (, address ds) = moduleCore.swapAsset(id, 1);
        Asset(ds).approve(address(moduleCore), type(uint256).max);
        moduleCore.depositPsm(id, 10e18);
        moduleCore.redeemRaWithDs(id, 1, 5e18);

        (uint112 raReserve, uint112 ctReserve) = flashSwapRouter.getAmmReserve(id, 1);
        console2.log("RA reserve in the amm pair contract: ", raReserve);

        uint256 actualWETHBalance = WETH.balanceOf(address(moduleCore));
        console2.log("Actual balance of WETH in the module core contract: ", actualWETHBalance);
        console2.log("Accounting RA locked, WETH in this case: ", moduleCore.valueLocked(id));

        assert(moduleCore.valueLocked(id) > (actualWETHBalance + raReserve));
        console2.log("Locked WETH + balance of alice: ", moduleCore.valueLocked(id) + WETH.balanceOf(alice));
        vm.stopPrank();
    }
```

```solidity
Logs:
  RA reserve in the amm pair contract:  118421052631578948
  Actual balance of WETH in the module core contract:  5131578947368421052
  Accounting RA locked, WETH in this case:  5381578947368421052
  Locked WETH + balance of alice:  10131578947368421052
```
To run the test use: ``forge test -vvv --mt test_RedeemRaWithDsIncorectRaUpdate``

### Mitigation
In the [PsmLib::_afterRedeemWithDs()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/PsmLib.sol#L329-L347) function, decrease the RA accounting with the fee. Add the below line after the fee is calculated.
```solidity
self.psm.balances.ra.decLocked(fee);
```