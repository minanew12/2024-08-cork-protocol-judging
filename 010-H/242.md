Colossal Magenta Elk

High

# If not all users have requested to redeem their LV tokens, some PA tokens will be locked in the protocol forever

### Summary

The Corc protocol allows users to deposit RA tokens in order to get back CT and DS tokens via the [Psm::depositPsm()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Psm.sol#L90-L101) function. CT and DS tokens have an expiration. There can be several DS and CT tokens minted for a RA:PA pair, but there is only one LV token per RA:PA pair. Users can mint the LV token by depositing the RA asset via the [Vault::depositLv()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Vault.sol#L33-L37) function. The users also have the possibility to purchase RA tokens from the protocol by depositing DS and PA tokens via the [Psm::redeemRaWithDs()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Psm.sol#L142-L159) function. When the CT and DS tokens have expired the protocol can issue new CT and DS tokens for the pair of RA:PA tokens. Users who have CT tokens can redeem a proportionate amount of the RA and PA tokens that are locked in the protocol via the [Psm::redeemWithCT()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Psm.sol#L198-L210) function. The other part of the RA tokens that reside in the contract, based on the LV tokens minted, will be split to RA and the new CT token and provided as liquidity to the newly create UniV2 pair for the CT and RA tokens. In order to issue new DS and CT tokens for a pair of RA and PA assets the protocol has to call the [ModuleCore::issueNewDs()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/ModuleCore.sol#L57-L86) function. There are a couple of functions that are called by the [ModuleCore::issueNewDs()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/ModuleCore.sol#L57-L86) function, one of them is the [VaultLib::onNewIssuance()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/VaultLib.sol#L92-L108) function, which in turn calls the [VaultLib::_liquidatedLp()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/VaultLib.sol#L349-L393) function:
```solidity
    function _liquidatedLp(
        State storage self,
        uint256 dsId,
        IUniswapV2Router02 ammRouter,
        IDsFlashSwapCore flashSwapRouter
    ) internal {
        DepegSwap storage ds = self.ds[dsId];

        // if there's no LP, then there's nothing to liquidate
        if (self.vault.config.lpBalance == 0) {
            return;
        }

        // the following things should happen here(taken directly from the whitepaper) :
        // 1. The AMM LP is redeemed to receive CT + RA
        // 2. Any excess DS in the LV is paired with CT to redeem RA
        // 3. The excess CT is used to claim RA + PA in the PSM
        // 4. End state: Only RA + redeemed PA remains

        self.vault.lpLiquidated.set(dsId);

        (uint256 raAmm, uint256 ctAmm) = __liquidateUnchecked(
            self, self.info.pair1, self.ds[dsId].ct, ammRouter, IUniswapV2Pair(ds.ammPair), self.vault.config.lpBalance
        );

        uint256 reservedDs = flashSwapRouter.emptyReserve(self.info.toId(), dsId);

        uint256 redeemAmount = reservedDs >= ctAmm ? ctAmm : reservedDs;
        PsmLibrary.lvRedeemRaWithCtDs(self, redeemAmount, dsId);

        // if the reserved DS is more than the CT that's available from liquidating the AMM LP
        // then there's no CT we can use to effectively redeem RA + PA from the PSM
        uint256 ctAttributedToPa = reservedDs >= ctAmm ? 0 : ctAmm - reservedDs;

        uint256 psmPa;
        uint256 psmRa;

        if (ctAttributedToPa != 0) {
            (psmPa, psmRa) = PsmLibrary.lvRedeemRaPaWithCt(self, ctAttributedToPa, dsId);
        }

        psmRa += redeemAmount;

        self.vault.pool.reserve(self.vault.lv.totalIssued(), raAmm + psmRa, psmPa);
    }
```
Now there is a separate issue with that function, where ``uint256 reservedDs = flashSwapRouter.emptyReserve(self.info.toId(), dsId);`` will always return 0, this goes against the expected behavior of the function described both in the comments and docs. , plus more PA assets will be distributed to the LV holders than it should, and in some cases those PA tokens will be lost. However if users have traded in the RA:CT UniV2 pair, and now the ctRatio is higher, this means that more CT tokens will be returned when the LP of the protocol is burned. When the CT tokens returned from liquidations are more than the DS reserve the [Psm::lvRedeemRaPaWithCt()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/PsmLib.sol#L130-L143) function will be called, and if there are PA tokens in the contract, part of the will be redeemed towards the LV holders. However when the [VaultPool::reserve()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/VaultPoolLib.sol#L12-L35) function is called:
```solidity
    function reserve(VaultPool storage self, uint256 totalLvIssued, uint256 addedRa, uint256 addedPa) internal {
        uint256 totalLvWithdrawn = self.withdrawalPool.atrributedLv;
        self.withdrawalPool.atrributedLv = totalLvWithdrawn;

        // RA
        uint256 totalRa = self.withdrawalPool.raBalance + addedRa;
        (uint256 attributedToWithdraw, uint256 attributedToAmm, uint256 ratePerLv) =
            MathHelper.separateLiquidity(totalRa, totalLvIssued, totalLvWithdrawn);

        self.withdrawalPool.raBalance = attributedToWithdraw;
        self.ammLiquidityPool.balance = attributedToAmm;
        self.withdrawalPool.raExchangeRate = ratePerLv;

        // PA
        uint256 totalPa = self.withdrawalPool.paBalance + addedPa;
        (attributedToWithdraw, attributedToAmm, ratePerLv) =
            MathHelper.separateLiquidity(totalPa, totalLvIssued, totalLvWithdrawn);

        self.withdrawalPool.paBalance = attributedToWithdraw;
        self.withdrawalPool.stagnatedPaBalance = attributedToWithdraw;
        self.withdrawalPool.paExchangeRate = ratePerLv;

        assert(totalRa == self.withdrawalPool.raBalance + self.ammLiquidityPool.balance);
    }
```
As can be seen from the above code snippet if the LV holders haven't requested to redeem their LV tokens, all of the PA tokens that should be distributed to them as rewards will be saved as **self.withdrawalPool.stagnatedPaBalance**. Contrary to the RA tokens that are saved in the **self.ammLiquidityPool.balance**, if users haven't requested to redeem their LV, the RA tokens will be used as liquidity for the next UniV2 pair. Basically the PA tokens that are saved as **self.withdrawalPool.stagnatedPaBalance** will be locked in the contract forever, as nobody can claim them, and they won't be used towards the liquidity in the newly created RA:CT UniV2 pairs. 

### Root Cause

In the [VaultPool::reserve()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/VaultPoolLib.sol#L12-L35) function, PA tokens that should be distributed towards new CT:RA UniV2 pairs, are being locked forever. 

### Internal pre-conditions

_No response_

### External pre-conditions

_No response_

### Attack Path

_No response_

### Impact

If users don't request to redeem their LV tokens, part of the PA tokens that they should eventually receive when they decide to redeem their LV tokens will be locked in the contract forever. Nobody can redeem them. Essentially users are loosing rewards, that they should receive, those rewards are taken from other users who can claim their expired CT tokens via the [Psm::redeemWithCT()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Psm.sol#L198-L204) function.

### PoC
In order to better illustrate the issue, the issue regarding ``uint256 reservedDs = flashSwapRouter.emptyReserve(self.info.toId(), dsId);`` will always returning 0, is not fixed. However as explained above if the returned CT amounts is bigger than the DS reserves according to the docs, the [Psm::lvRedeemRaPaWithCt()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/PsmLib.sol#L130-L143) function will still be called. The percentage of lost funds will probably be smaller, but that depends on a lot of factors. Nevertheless if the protocol operates with PA assets worth millions, the loses will enormous. 
[Gist](https://gist.github.com/AtanasDimulski/3f9bfc84c63e1c977b877613b644c0e2)
After following the steps in the above mentioned [gist](https://gist.github.com/AtanasDimulski/3f9bfc84c63e1c977b877613b644c0e2) add the following test to the ``AuditorTests.t.sol`` file:

```solidity
    function test_PARewardsAreLost() public {
        vm.startPrank(owner);
        /// @notice Set the repruchase fee to 0 for easier calculations
        corkConfig.updateRepurchaseFeeRate(id, 0);

        /// @notice Set the redeemRa fee to 0 for easier calculations
        corkConfig.updatePsmBaseRedemptionFeePrecentage(0);
        vm.stopPrank();

        vm.startPrank(alice);
        WETH.mint(alice, 10e18);
        WETH.approve(address(moduleCore), type(uint256).max);
        moduleCore.depositPsm(id, 10e18);
        vm.stopPrank();

        vm.startPrank(tom);
        WETH.mint(tom, 10e18);
        WETH.approve(address(moduleCore), type(uint256).max);
        peggedAsset.mint(tom, 10e18);
        peggedAsset.approve(address(moduleCore), type(uint256).max);
        moduleCore.depositPsm(id, 10e18);
        (address ct, address ds) = moduleCore.swapAsset(id, 1);
        Asset(ds).approve(address(moduleCore), type(uint256).max);
        moduleCore.redeemRaWithDs(id, 1, 10e18);
        console2.log("Tom's WETH balance: ", WETH.balanceOf(tom));
        console2.log("Tom's CT balance: ", Asset(ct).balanceOf(tom));
        vm.stopPrank();

        vm.startPrank(bob);
        WETH.mint(bob, 10e18);
        WETH.approve(address(moduleCore), type(uint256).max);
        moduleCore.depositLv(id, 10e18);
        Asset(lvAddress).approve(address(moduleCore), type(uint256).max);
        vm.stopPrank();

        skip(1100);

        vm.startPrank(owner);
        corkConfig.issueNewDs(id, block.timestamp + 1100, 1e18, 0);
        vm.stopPrank();

        vm.startPrank(bob);
        moduleCore.requestRedemption(id, 10e18);
        vm.stopPrank();

        vm.startPrank(alice);
        Asset(ct).approve(address(moduleCore), type(uint256).max);
        moduleCore.redeemWithCT(id, 1, 10e18);
        vm.stopPrank();

        vm.startPrank(tom);
        Asset(ct).approve(address(moduleCore), type(uint256).max);
        moduleCore.redeemWithCT(id, 1, 10e18);
        vm.stopPrank();

        skip(1200);

        vm.startPrank(bob);
        moduleCore.redeemExpiredLv(id, bob, 10e18);
        console2.log("PA asset balance of the moduleCore contract: ", peggedAsset.balanceOf(address(moduleCore)));
        console2.log("RA asset balance of the moduleCore contract: ", WETH.balanceOf(address(moduleCore)));
        vm.stopPrank();
    }
```

```solidity
Logs:
  Tom's WETH balance:  10000000000000000000
  Tom's CT balance:  10000000000000000000
  PA asset balance of the moduleCore contract:  2083333333333333333
  RA asset balance of the moduleCore contract:  1699
```
As can be seen from the above logs, after the users have redeemed everything that they can redeem, there is **≈2.08e18 PA** assets locked in the contract. If the holders of LV tokens have requested to redeem their LV while the first CT and DS tokens haven't expired, those **≈2.08e18 PA**  tokens would have been distributed to them. 

To run the test use: ``forge test -vvv --mt test_PARewardsAreLost``

### Mitigation

_No response_