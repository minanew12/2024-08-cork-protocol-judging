Blurry Blush Mouse

High

# Anyone can trigger `VaultLib::_liquidatedLp()` which does not call `lvRedeemRaWithCtDs()` with the correct amount and will lead to incorrect withdrawals and stuck funds

### Summary

[VaultLib::_liquidatedLp()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/VaultLib.sol#L349) is triggered when the expiry date is reached, by calling [VaultLib::redeemExpired()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/VaultLib.sol#L514) or [VaultLib::onNewIssuance()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/VaultLib.sol#L92). The purpose is liquidating all the assets and turn them into withdrawable funds for users, by calling [VaultPoolLib::reserve()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/VaultLib.sol#L392) and adding funds [self.withdrawalPool and self.ammLiquidityPool](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/VaultPoolLib.sol#L21-L32).

However, there is a bug with the [redeemAmount](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/VaultLib.sol#L376) passed to [PsmLib::.lvRedeemRaWithCtDs()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/VaultLib.sol#L377), as it will always be 0 due to [FlashSwapRouter::emptyReserve()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/VaultLib.sol#L374) always returning [0](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/flash-swaps/FlashSwapRouter.sol#L70), which means Ra will never be redeemed for `Ct` and `Ds`. 

It returns `0` because [DsFlashSwap::emptyReserve() ](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/DsFlashSwap.sol#L67) calls [DsFlashSwap::emptyReservePartial()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/DsFlashSwap.sol#L67) with the full reserve, `self.ds[dsId].reserve` and `DsFlashSwap::emptyReservePartial()` returns the reserve [post reduction](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/DsFlashSwap.sol#L77), which is 0 in this case.

Additionally, the same logical error happens in [VaultLib::_redeemCtDsAndSellExcessCt()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/VaultLib.sol#L329), which in this case means the [ctSellAmount](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/VaultLib.sol#L334) will be bigger than it should and it will try to [sell](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/VaultLib.sol#L345) more `ct` than it has and revert.

### Root Cause

In `VaultLib.sol:329`, `reservedDs` is the reserve amount post reduction, when it should be the amount pre reduction.
In `VaultLib.sol:374`, the same happens (is always 0 in this case).

### Internal pre-conditions

None.

### External pre-conditions

None.

### Attack Path

Admin calls [ModuleCore::issueNewDs()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/ModuleCore.sol#L57), which calls [VaultLib::onNewIssuance()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/ModuleCore.sol#L83) and then `VaultLib::_liquidatedLp()`.
Or an user calls [Vault::redeemExpiredLv()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Vault.sol#L109) which calls [VaultLib::redeemExpired()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/core/Vault.sol#L116), calling `VaultLib::_liquidatedLp()` internally.

### Impact

In case the bug happens on `VaultLib::_liquidatedLp()`, users will get less `RA` then they should as `PsmLib::lvRedeemRaWithCtDs()` will be called with 0 amount and all `ct` will be incorrectly redeemed for `Ra` and `Pa` in `PsmLib::lvRedeemRaPaWithCt()`, splitting the funds with all `ct` token holders.
In case the bug happens on `VaultLib::__redeemCtDsAndSellExcessCt()`, users will be DoSed from redeem earling, as it is part of the [VaultLib::redeemEarly()](https://github.com/sherlock-audit/2024-08-cork-protocol/blob/main/Depeg-swap/contracts/libraries/VaultLib.sol#L639) flow and will revert.

### PoC

`VaultLib.sol`

```solidity
function _redeemCtDsAndSellExcessCt(
    State storage self,
    uint256 dsId,
    IUniswapV2Router02 ammRouter,
    IDsFlashSwapCore flashSwapRouter,
    uint256 ammCtBalance
) internal returns (uint256 ra) {
    ...
    reservedDs = flashSwapRouter.emptyReservePartial(self.info.toId(), dsId, redeemAmount); //@audit this amount will be smaller than it should

    ra += redeemAmount;
    PsmLibrary.lvRedeemRaWithCtDs(self, redeemAmount, dsId);

    uint256 ctSellAmount = reservedDs >= ammCtBalance ? 0 : ammCtBalance - reservedDs; 
    ...
}

function _liquidatedLp(
    State storage self,
    uint256 dsId,
    IUniswapV2Router02 ammRouter,
    IDsFlashSwapCore flashSwapRouter
) internal {
    ...
    uint256 reservedDs = flashSwapRouter.emptyReserve(self.info.toId(), dsId); //@audit this will be 0

    uint256 redeemAmount = reservedDs >= ctAmm ? ctAmm : reservedDs;
    PsmLibrary.lvRedeemRaWithCtDs(self, redeemAmount, dsId);
    ...
}
```

`FlashSwapRouter.sol`

```solidity
function emptyReserve(Id reserveId, uint256 dsId) external override onlyOwner returns (uint256 amount) {
    amount = reserves[reserveId].emptyReserve(dsId, owner());
    emit ReserveEmptied(reserveId, dsId, amount);
}

function emptyReservePartial(Id reserveId, uint256 dsId, uint256 amount)
    external
    override
    onlyOwner
    returns (uint256 reserve)
{
    reserve = reserves[reserveId].emptyReservePartial(dsId, amount, owner());
    emit ReserveEmptied(reserveId, dsId, amount);
}
```

`DsFlashSwap.sol`

```solidity
function emptyReserve(ReserveState storage self, uint256 dsId, address to) internal returns (uint256 reserve) {
    reserve = emptyReservePartial(self, dsId, self.ds[dsId].reserve, to);
}

function emptyReservePartial(ReserveState storage self, uint256 dsId, uint256 amount, address to)
    internal
    returns (uint256 reserve)
{
    self.ds[dsId].ds.transfer(to, amount);

    self.ds[dsId].reserve -= amount;
    reserve = self.ds[dsId].reserve; //@audit returns reserve post reduction
}
```

### Mitigation

Fetch the reserve pre reduction and use that value to calculate `reservedDs`.